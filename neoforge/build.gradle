plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    maven { url 'https://api.modrinth.com/maven' }
    maven { url "https://maven.shedaniel.me/" }
    maven {
        name "KosmX's maven"
        url 'https://maven.kosmx.dev/'
    }
}

dependencies {
    implementation 'maven.modrinth:better-combat:2.3.0+1.21.1-neoforge'
    implementation "me.shedaniel.cloth:cloth-config-neoforge:15.0.140"
    implementation "dev.kosmx.player-anim:player-animation-lib-forge:2.0.1+1.21.1"

    jarJar(group: 'org.apache.xmlgraphics', name: 'batik-all', version: '[1.16,1.17)')
    jarJar(group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '[2.7,2.8)')
    jarJar(group: 'xml-apis', name: 'xml-apis', version: '[1.4.01,1.5)')
    jarJar(group: 'xml-apis', name: 'xml-apis-ext', version: '[1.3.04,1.4)')
    jarJar(group: 'net.imagej', name: 'ij', version: '[1.54f,1.55)')

    implementation 'org.apache.xmlgraphics:batik-all:1.16'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.7'
    implementation 'xml-apis:xml-apis:1.4.01'
    implementation 'xml-apis:xml-apis-ext:1.3.04'
    implementation 'net.imagej:ij:1.54f'

    additionalRuntimeClasspath 'org.apache.xmlgraphics:batik-all:1.16'
    additionalRuntimeClasspath 'net.imagej:ij:1.54f'
}

configurations.all {
    exclude group: 'xml-apis', module: 'xml-apis'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
